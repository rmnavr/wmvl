
	(* TODO:
	   - instead of UpdDir button, pass fileName only, and construct fullPath everytime via NotebookDirection
	   - TOOLTIP upd String?

	   INFO:
	   - for Initialization to work correctly, probably wait 2..3 sec before «Enable Dynamics»
	*)

(* Import funcs ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{1 *)

	(* <| "Out" -> {"String", ""} |> *)
	wmvl`generateFullFileName[fileName_String] := NotebookDirectory[] <> fileName ;

	(* <| "Out" -> {"IO", "erases all data from file"} |> *)
    wmvl`clearFileContent[fileName_String] := Export[wmvl`generateFullFileName[fileName], ""];

	(* <| "Out" -> {"IO", "deleted cells from Notebook"} |> *)
    wmvl`deleteTaggedCells[] := Module[
		{}
	  , NotebookLocate[wmvl`$Tag] (* select prev imported cells *)
      ; NotebookDelete[SelectedNotebook[]] (* and delete them *)
    ];

	(* <| "Out" -> {"Integer", ""} |> *)
    wmvl`calcFontSize[nLines_Integer] := Module[
	   {nSmall, nBig, fontMin, fontMax, k, b}
	  , nSmall  = 10
      ; nBig    = 50
      ; fontMin = 2
      ; fontMax = 13
      ; k = -(fontMax - fontMin)/(nBig - nSmall)
      ; b = fontMax - k*nSmall
      ; If[nLines < nSmall, Return @ fontMax, Nothing]
      ; If[nLines > nBig  , Return @ fontMin, Nothing]
      ; Return @ Round @ (k*nLines + b)
    ];

	(* <| "Out" -> {"IO", "read file content, put it into new cell, execute"} |> *)
    wmvl`importFileToNewCellAndExecute[fileName_String] := Module[
        {importedText}
      , SelectionMove[InputNotebook[], After, Notebook] (* go to EOF *)
      ; NotebookWrite[
            EvaluationNotebook[]
          , Cell[
                importedText = Import[wmvl`generateFullFileName[fileName], "text"]
                (*why not "nb" — it imports and launches, why not "string" — it makes extra «\n» symbols*)
              , "Input"
              , CellTags -> wmvl`$Tag
			]
          , All (*select all what was written*)
		]
      ; SetOptions[
            SelectedCells[][[1]]
          , FontSize -> wmvl`calcFontSize[StringCount[importedText, "\n"]]
          , LineSpacing -> {0.7, 0}
          , Background -> LightBlue
          , CellFrameColor -> Blue
          , CellDingbat ->
                Cell[
                    BoxData[StyleBox["⮸ ", FontColor -> GrayLevel[0.1]]]
                  , Background -> {}
                  , FontSize -> 15
                ]
        ]
      ; FrontEndTokenExecute[EvaluationNotebook[], "Style", "Input"]
      ; FrontEndTokenExecute[EvaluationNotebook[], "Evaluate"]
      ; FrontEndTokenExecute[EvaluationNotebook[], "ScrollLineUp"]   (* \ forced redraw *) 
      ; FrontEndTokenExecute[EvaluationNotebook[], "ScrollLineDown"] (* /				*)
    ];

(* ____________________________________________________________________________/ }}}1 *)
(* Status Cell ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{1 *)

	(* <| "Out" -> {"Cell", "docked"} |> *)
    wmvl`constructStatusCell[fileName_String] :=
        Cell[
            BoxData[ToBoxes[
				wmvl`constructStatusCellContent[fileName]
            ]]
          , "DockedCell"
          , CellFrame -> {{0, 0}, {2, 0}}
          , CellFrameMargins -> {{8, 8}, {4, 4}}
          , CellFrameColor -> Darker @ Gray
        ];

	(* <| "Out" -> {"DynamicModule", ""} |> *)
    wmvl`constructStatusCellContent[fileName_String] :=
        DynamicModule[
			{tooltip}
		  , Row[{
			    " "
			  , {Print @ 1, Style["WMVL v250303", Bold]} //
				Framed[#, RoundingRadius -> 5, Background -> LightBlue, FrameMargins -> 2, ImageMargins -> 0]& //
				Tooltip[#, Row[{"Exchange File: ", Style[wmvl`generateFullFileName[fileName], Bold]}]]& //
				Dynamic (* needed for tooltip regeneration when NotebookDir changes *)
			  , " | "
			  , wmvl`readerModule[fileName]
			  , " | "
			  ,	wmvl`constructDelAllCellsButton[], " "
			  , wmvl`constructPurgeButton[], " "
			  , wmvl`constructFEAbortButton[]
			  , " | "
			  , wmvl`constructUpdDirButton[]
			}]
		  , SaveDefinitions -> True
		  , Initialization :> {wmvl`clearFileContent[fileName]}
        ];

(* ■ Buttons ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{2 *)

	(* <| "Out" -> {"Button", ""} |> *)
	wmvl`constructFEAbortButton[] := Button["Abort Evaluation", FrontEndTokenExecute[EvaluationNotebook[], "EvaluatorAbort"], ImageMargins -> 0] ;

	(* <| "Out" -> {"Button", ""} |> *)
	wmvl`constructDelAllCellsButton[] := Button[
		"Delete all Cells"
	  , ( SelectionMove[EvaluationNotebook[], All, Notebook]
	    ; FrontEndTokenExecute[EvaluationNotebook[], "DeleteNext"]
		)
	  , ImageMargins -> 0
	  ] ;

	(* <| "Out" -> {"Button", ""} |> *)
	wmvl`constructPurgeButton[] := Button["Remove all from Global`", Remove["Global`*"] // Quiet, ImageMargins -> 0] ;

	(* <| "Out" -> {"Button", ""} |> *)
	wmvl`constructUpdDirButton[] := Button[
		"Relaunch WMVL"
	  , wmvl`launchPlugin[wmvl`exchangeFile]
	  , ImageMargins -> 0
	] ;

(* _______________________________________________________________________/ }}}2 *)
(* ■ Reader ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{2 *)

	(* <| "Out" -> {"Button", ""} |> *)
	wmvl`constructReadNowButton[fileName_String] :=
		Button[
			"Read now"
		   , ( wmvl`deleteTaggedCells[]
			 ; wmvl`importFileToNewCellAndExecute[fileName]
			 )
		   , ImageMargins -> 0, FrameMargins -> 0
		]

	(* <| "Out" -> {"Row", ""} |> *)
    wmvl`readerModule[fileName_String] := DynamicModule[
		{ dynLastChangedTime = FileDate[wmvl`generateFullFileName[fileName]]
		, dynActive = True
		, tick = "/ "
		}
	  , Row[{
			Checkbox[Dynamic[dynActive]], Toggler[Dynamic[dynActive], {True -> " AutoRead | ", False -> " AutoRead | "}]
		  , Dynamic[
				If[ dynActive
				  , Refresh[
						(* file read actions (at every UpdateInterval): *)
						If[ FileDate[wmvl`generateFullFileName[fileName]] > dynLastChangedTime
						  , { wmvl`deleteTaggedCells[]
							, wmvl`importFileToNewCellAndExecute[fileName]
							, dynLastChangedTime = FileDate[wmvl`generateFullFileName[fileName]]
							}
						  , Nothing
						]   
						(* visuals: *)
					  ; Row[{   
						  ,	"Last exchange file upd: "
						  , DateString[FileDate[wmvl`generateFullFileName[fileName]]]
						  , " "
						  , (If[tick == "/ ", tick = "\\ ", tick = "/ "]; tick)
						}]
					  , UpdateInterval -> 3
					]
				  , "---- -------- ---- ---: --- -- --- ---- --:--:-- - "
				]
			  , TrackedSymbols :> {dynActive}
			]
		  , " "
		  , wmvl`constructReadNowButton[fileName]
		}] //
		UnderBar
	]

(* _______________________________________________________________________/ }}}2 *)

(* ____________________________________________________________________________/ }}}1 *)
(* Launch ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{1 *)

	(* <| "Out" -> {"IO", ""} |> *)
    wmvl`launchPlugin[fileName_String (* echange file *)] := Module[
		{dockedCell}
	  , wmvl`clearFileContent[fileName]
	  ; dockedCell = wmvl`constructStatusCell[fileName]
      ; SetOptions[
            InputNotebook[]
          , DockedCells -> dockedCell
        ];
    ];

(* ____________________________________________________________________________/ }}}1 *)
(* Leftovers ‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾\ {{{2 *)

	wmvl`blinkerNOTUSEDCURRENTLY = 
		DynamicModule[
			{t, show, hide}
		  , show = Style["•", Green]
		  ; hide = Style[".", Gray]
		  ; Dynamic[
				Refresh[
					t = 0;
					Dynamic @
						If[ t == -1
						  , hide
						  , If[ t < 100
							  , (t++; show)
							  , (t = -1; hide)
							]
						]
				]
			  , UpdateInterval -> 3
			  , TrackedSymbols :> {}
			]
		] ;

(* ____________________________________________________________________________/ }}}1 *)

	wmvl`$Tag = "VimIO" ;
    wmvl`exchangeFile = "_WMVimLink_tmp.txt" ;
    wmvl`launchPlugin[wmvl`exchangeFile] ;

